# Copyright 2022 Google LLC
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

"""
    AWS Proxy for the Nitro Enclave

    Note: this package uses an older version of absl. This is to work around
    the incompatibility between the clang-11 compiler used on amazonlinux2
    and the versions of absl since 2023-11-17 (commit 00e087f). clang-11
    doesn't have std::filesystem, instead it's in std::experimental
"""

load("@com_google_googleapis_imports//:imports.bzl", "cc_grpc_library", "cc_proto_library")
load("@rules_buf//buf:defs.bzl", "buf_lint_test")
load("@rules_cc//cc:defs.bzl", "cc_binary", "cc_library", "cc_test")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_load")
load("@rules_pkg//pkg:mappings.bzl", "pkg_attributes", "pkg_files")
load("@rules_pkg//pkg:pkg.bzl", "pkg_tar")
load("@rules_proto//proto:defs.bzl", "proto_library")
load("@rules_python//python:defs.bzl", "py_test")

cc_binary(
    name = "resolv_conf_getter_client",
    srcs = ["resolv_conf_getter_client.cc"],
    deps = [
        ":resolv_conf_getter_grpc",
        "@com_github_grpc_grpc//:grpc++",
        "@com_google_absl//absl/flags:flag",
        "@com_google_absl//absl/flags:parse",
        "@com_google_absl//absl/status:statusor",
        "@com_google_absl//absl/strings",
    ],
)

cc_binary(
    name = "resolv_conf_getter_server",
    srcs = ["resolv_conf_getter_server.cc"],
    deps = [
        ":resolv_conf_getter_grpc",
        "@com_github_grpc_grpc//:grpc++",
        "@com_google_absl_for_proxy//absl/strings",
    ],
)

proto_library(
    name = "resolv_conf_getter_proto",
    srcs = ["resolv_conf_getter.proto"],
)

cc_proto_library(
    name = "resolv_conf_getter_cc_proto",
    deps = [":resolv_conf_getter_proto"],
)

cc_grpc_library(
    name = "resolv_conf_getter_grpc",
    srcs = [":resolv_conf_getter_proto"],
    grpc_only = True,
    deps = [":resolv_conf_getter_cc_proto"],
)

buf_lint_test(
    name = "resolv_conf_getter_proto_lint",
    size = "small",
    config = "//src:buf.yaml",
    targets = [":resolv_conf_getter_proto"],
)

pkg_files(
    name = "resolv_conf_getter_server_bin",
    srcs = [":resolv_conf_getter_server"],
    attributes = pkg_attributes(mode = "0555"),
    prefix = "/opt/google/",
)

pkg_tar(
    name = "resolv_conf_getter_server_tar",
    srcs = [":resolv_conf_getter_server_bin"],
)

oci_image(
    name = "resolv_conf_getter_server_debian_image",
    base = select({
        "@platforms//cpu:aarch64": "@runtime-debian-debug-nonroot-arm64",
        "@platforms//cpu:x86_64": "@runtime-debian-debug-nonroot-amd64",
    }),
    entrypoint = ["/opt/google/resolv_conf_getter_server"],
    exposed_ports = ["1600"],
    tars = [
        ":resolv_conf_getter_server_tar",
    ],
    visibility = [
        "//production/packaging/aws:__subpackages__",
    ],
)

oci_load(
    name = "resolv_conf_getter_server_debian_tarball",
    image = ":resolv_conf_getter_server_debian_image",
    repo_tags = ["src/aws/proxy:resolv_conf_getter_server_debian"],
)

filegroup(
    name = "resolv_conf_getter_server_debian_tarball.tar",
    srcs = [":resolv_conf_getter_server_debian_tarball"],
    output_group = "tarball",
)

genrule(
    name = "copy_to_dist",
    srcs = [":resolv_conf_getter_server_debian_tarball.tar"],
    outs = ["copy_to_dist.bin"],
    cmd_bash = """cat << EOF > '$@'
readonly target_dir=dist/aws
mkdir -p \\$${target_dir}
cp $(execpath :resolv_conf_getter_server_debian_tarball.tar) "\\$${target_dir}"
EOF""",
    executable = True,
    local = True,
    message = "copying resolv_conf_getter_server to dist/aws/ directory",
)

cc_library(
    name = "acceptor_pool",
    hdrs = ["acceptor_pool.h"],
    deps = [
        ":socket_types",
        "@com_google_absl_for_proxy//absl/container:flat_hash_map",
        "@com_google_absl_for_proxy//absl/log",
    ],
)

cc_library(
    name = "buffer",
    hdrs = ["buffer.h"],
    deps = [
        ":freelist",
    ],
)

cc_library(
    name = "client_session_pool",
    srcs = ["client_session_pool.cc"],
    hdrs = ["client_session_pool.h"],
    deps = [
        ":socket_types",
        ":socket_vendor_protocol",
        "@boost//:asio",
        "@com_google_absl_for_proxy//absl/log",
    ],
)

cc_library(
    name = "config",
    hdrs = ["config.h"],
)

cc_library(
    name = "freelist",
    hdrs = ["freelist.h"],
)

cc_library(
    name = "preload",
    srcs = [
        "preload.cc",
    ],
    hdrs = ["preload.h"],
    deps = [
        ":protocol",
        ":socket_vendor_protocol",
    ],
)

cc_library(
    name = "protocol",
    srcs = ["protocol.cc"],
    hdrs = ["protocol.h"],
)

cc_library(
    name = "proxy_bridge",
    srcs = ["proxy_bridge.cc"],
    hdrs = ["proxy_bridge.h"],
    deps = [
        ":acceptor_pool",
        ":buffer",
        ":socket_types",
        ":socks5_state",
        "@boost//:asio",
        "@com_google_absl_for_proxy//absl/log",
    ],
)

cc_library(
    name = "proxy_server",
    srcs = ["proxy_server.cc"],
    hdrs = ["proxy_server.h"],
    deps = [
        ":acceptor_pool",
        ":config",
        ":proxy_bridge",
        ":socket_types",
        "@boost//:asio",
    ],
)

cc_library(
    name = "socket_types",
    hdrs = ["socket_types.h"],
)

cc_library(
    name = "socket_vendor_protocol",
    hdrs = ["socket_vendor_protocol.h"],
)

cc_library(
    name = "socket_vendor_server",
    srcs = ["socket_vendor_server.cc"],
    hdrs = ["socket_vendor_server.h"],
    deps = [
        ":client_session_pool",
        ":socket_types",
        "@boost//:asio",
        "@com_google_absl_for_proxy//absl/log",
        "@com_google_absl_for_proxy//absl/log:check",
    ],
)

cc_library(
    name = "socks5_state",
    srcs = ["socks5_state.cc"],
    hdrs = ["socks5_state.h"],
    deps = [
        ":buffer",
        ":protocol",
        "@com_google_absl_for_proxy//absl/log",
    ],
)

cc_binary(
    name = "proxy",
    srcs = [":proxy.cc"],
    linkopts = ["-pthread"],
    visibility = ["//visibility:public"],
    deps = [
        ":config",
        ":proxy_server",
        "@com_google_absl_for_proxy//absl/base:log_severity",
        "@com_google_absl_for_proxy//absl/debugging:failure_signal_handler",
        "@com_google_absl_for_proxy//absl/debugging:symbolize",
        "@com_google_absl_for_proxy//absl/flags:flag",
        "@com_google_absl_for_proxy//absl/flags:parse",
        "@com_google_absl_for_proxy//absl/log",
        "@com_google_absl_for_proxy//absl/log:check",
        "@com_google_absl_for_proxy//absl/log:flags",
        "@com_google_absl_for_proxy//absl/log:globals",
        "@com_google_absl_for_proxy//absl/log:initialize",
    ],
)

cc_binary(
    name = "proxy_preload",
    srcs = [
        ":preload.cc",
        ":protocol.cc",
        ":protocol.h",
    ],
    copts = [
        "-fvisibility=hidden",
    ],
    linkopts = ["-ldl"],
    linkshared = True,
    visibility = [
        "//production/packaging/aws:__subpackages__",
    ],
    deps = [
        ":preload",
        ":protocol",
        ":socket_vendor_protocol",
    ],
)

cc_binary(
    name = "proxify",
    srcs = [":proxify.cc"],
    visibility = [
        "//production/packaging/aws:__subpackages__",
    ],
    deps = [
        "@com_google_absl_for_proxy//absl/flags:parse",
        "@com_google_absl_for_proxy//absl/strings",
    ],
)

cc_binary(
    name = "socket_vendor",
    srcs = [":socket_vendor.cc"],
    copts = [
        "-Wno-c99-designator",
    ],
    visibility = [
        "//production/packaging/aws:__subpackages__",
    ],
    deps = [
        ":protocol",
        ":socket_vendor_server",
        "@com_google_absl_for_proxy//absl/flags:parse",
        "@com_google_absl_for_proxy//absl/log",
        "@com_google_absl_for_proxy//absl/log:flags",
        "@com_google_absl_for_proxy//absl/log:initialize",
        "@com_google_absl_for_proxy//absl/strings",
    ],
)

pkg_files(
    name = "proxy_bin",
    srcs = [":proxy"],
    attributes = pkg_attributes(mode = "0755"),
    prefix = "/opt/google/proxy",
)

pkg_tar(
    name = "proxy_tar",
    srcs = [":proxy_bin"],
)

oci_image(
    name = "proxy_debian_image",
    base = select({
        "@platforms//cpu:aarch64": "@runtime-debian-debug-root-arm64",
        "@platforms//cpu:x86_64": "@runtime-debian-debug-root-amd64",
    }),
    entrypoint = ["/bin/bash"],
    tars = [
        ":proxy_tar",
    ],
    visibility = [
        "//production/packaging/aws:__subpackages__",
        "//src/public/cpio/validator:__subpackages__",
    ],
)

oci_load(
    name = "proxy_debian_tarball",
    image = ":proxy_debian_image",
    repo_tags = ["src/aws/proxy:proxy_debian"],
    visibility = ["//visibility:public"],
)

filegroup(
    name = "proxy_debian_tarball.tar",
    srcs = [":proxy_debian_tarball"],
    output_group = "tarball",
    visibility = ["//visibility:public"],
)

pkg_files(
    name = "proxify_executables",
    srcs = [
        "//src/aws/proxy:proxify",
        "//src/aws/proxy:resolv_conf_getter_client",
        "//src/aws/proxy:socket_vendor",
    ],
    attributes = pkg_attributes(mode = "0555"),
    prefix = "/",
)

pkg_files(
    name = "proxify_libs",
    srcs = [":proxy_preload"],
    attributes = pkg_attributes(mode = "0444"),
    prefix = "/",
)

pkg_tar(
    name = "proxify_tar",
    srcs = [
        ":proxify_executables",
        ":proxify_libs",
    ],
)

pkg_files(
    name = "libnsm_libs",
    srcs = ["//src/cpio/client_providers/kms_client_provider/aws:libnsm_so"],
    attributes = pkg_attributes(mode = "0444"),
    prefix = "/usr/lib/x86_64-linux-gnu/",
)

pkg_tar(
    name = "libnsm_and_proxify_tar",
    srcs = [
        ":libnsm_libs",
        ":proxify_executables",
        ":proxify_libs",
    ],
    visibility = ["//visibility:public"],  # Leave as public for KV server.
)

cc_test(
    name = "socks5_state_test",
    size = "small",
    srcs = ["socks5_state_test.cc"],
    deps = [
        ":buffer",
        ":socks5_state",
        "@com_google_googletest//:gtest_main",
    ],
)

cc_test(
    name = "socks5_state_input_test",
    size = "small",
    srcs = ["socks5_state_input_test.cc"],
    deps = [
        ":socks5_state",
        "@com_google_googletest//:gtest_main",
    ],
)

cc_test(
    name = "protocol_test",
    size = "small",
    srcs = ["protocol_test.cc"],
    deps = [
        ":protocol",
        "@com_google_googletest//:gtest_main",
    ],
)

cc_test(
    name = "preload_test",
    size = "small",
    srcs = ["preload_test.cc"],
    linkopts = ["-ldl"],
    deps = [
        ":preload",
        "@com_google_googletest//:gtest_main",
    ],
)

py_test(
    name = "socks5_test",
    size = "small",
    srcs = ["socks5_test.py"],
    args = ["$(location :proxy)"],
    data = [
        ":proxy",
    ],
)

sh_test(
    name = "preload_curl_test",
    size = "small",
    srcs = [":curl_client_test"],
    args = [
        "$(location :proxy)",
        "$(location :proxy_preload)",
        "$(location :proxify)",
    ],
    data = [
        ":proxify",
        ":proxy",
        ":proxy_preload",
        ":socket_vendor",
    ],
    tags = [
        "noasan",  # TODO(b/293876281) - Fix ASAN errors in this test.
        "notsan",  # TODO(b/307540489) - Fix TSAN errors in this test.
    ],
)

sh_test(
    name = "resolv_conf_getter_test",
    size = "small",
    srcs = ["resolv_conf_getter_test_app"],
    args = [
        "$(location :resolv_conf_getter_client)",
        "$(location :resolv_conf_getter_server)",
    ],
    data = [
        ":resolv_conf_getter_client",
        ":resolv_conf_getter_server",
    ],
)

sh_test(
    name = "preload_exported_symbols_test",
    size = "small",
    srcs = ["preload_exported_symbols"],
    args = [
        "$(location :proxy_preload)",
    ],
    data = [
        ":proxy_preload",
    ],
    tags = [
        # Ubsan adds symbol _ZSt3minIjERKT_S2_S2_, causing this test to fail
        "noubsan",
    ],
)

cc_test(
    name = "buffer_test",
    size = "small",
    srcs = ["buffer_test.cc"],
    deps = [
        ":buffer",
        "@com_google_googletest//:gtest_main",
    ],
)

cc_test(
    name = "freelist_test",
    size = "small",
    srcs = ["freelist_test.cc"],
    deps = [
        ":buffer",
        ":freelist",
        "@com_google_absl//absl/container:flat_hash_set",
        "@com_google_googletest//:gtest_main",
    ],
)

cc_test(
    name = "proxy_bridge_test",
    size = "small",
    srcs = ["proxy_bridge_test.cc"],
    deps = [
        ":proxy_bridge",
        "@com_google_googletest//:gtest_main",
    ],
)

cc_test(
    name = "proxy_server_test",
    size = "small",
    srcs = ["proxy_server_test.cc"],
    deps = [
        ":proxy_server",
        "@com_google_googletest//:gtest_main",
    ],
)

cc_test(
    name = "client_session_pool_test",
    size = "medium",
    timeout = "short",
    srcs = ["client_session_pool_test.cc"],
    tags = [
        "manual",  # TODO(b/315002386) - Fix test flakiness.
        "noasan",  # TODO(b/315002386) - Fix ASAN errors in this test.
    ],
    deps = [
        ":client_session_pool",
        ":socket_vendor_protocol",
        "@com_google_absl//absl/log",
        "@com_google_googletest//:gtest_main",
    ],
)

cc_test(
    name = "socket_vendor_server_test",
    size = "small",
    srcs = ["socket_vendor_server_test.cc"],
    deps = [
        ":socket_types",
        ":socket_vendor_server",
        "@com_google_googletest//:gtest_main",
    ],
)

cc_binary(
    name = "benchmark_test",
    srcs = ["benchmark_test.cc"],
    deps = [
        ":proxy_server",
        "@boost//:asio",
        "@google_benchmark//:benchmark",
    ],
)
