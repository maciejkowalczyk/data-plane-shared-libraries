# Copyright 2022 Google LLC
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

load("@bazel_skylib//rules:copy_file.bzl", "copy_file")
load("@rules_cc//cc:defs.bzl", "cc_binary", "cc_library", "cc_test")
load(
    "@rules_pkg//pkg:mappings.bzl",
    "pkg_attributes",
    "pkg_files",
)
load("@rules_pkg//pkg:tar.bzl", "pkg_tar")

package(default_visibility = ["//src:scp_internal_pkg"])

aws_nitro_kms_path = "/"

aws_nitro_kms_cli_name = "aws_nitro_enclaves_cli"

aws_nitro_kms_cli_path = aws_nitro_kms_path + aws_nitro_kms_cli_name

alias(
    name = "kms_cli",
    actual = "@nitrokmscli_aws_nitro_enclaves_sdk_c//:{}".format(aws_nitro_kms_cli_name),
    visibility = ["//visibility:public"],
)

# copy kms_cli target's output as an opaque executable, simulating a binary
# that's not built from source
copy_file(
    name = "kms_cli_exe",
    src = ":kms_cli",
    out = "kms_cli.exe",
    is_executable = True,
    visibility = ["//visibility:private"],
)

alias(
    name = "libnsm_so",
    actual = "@aws-nitro-enclaves-nsm-api//:nsm",
    visibility = ["//visibility:public"],
)

copy_file(
    name = "libnsm_so_lib",
    src = ":libnsm_so",
    out = "libnsm.so",
    is_executable = False,
    visibility = ["//visibility:private"],
)

sh_test(
    name = "kms_cli_exe_test",
    size = "small",
    srcs = [":kms_cli.exe"],
    args = [
        "decrypt",
        "--help",
    ],
    data = [":libnsm.so"],
    env = {"LD_LIBRARY_PATH": "."},
    tags = [
        "local",  # local precludes remote caching
        "noasan",
    ],
)

pkg_files(
    name = "kms_executables",
    srcs = [
        ":kms_cli",
    ],
    attributes = pkg_attributes(mode = "0555"),
    prefix = aws_nitro_kms_path,
)

pkg_files(
    name = "kms_libs",
    srcs = [
        ":libnsm_so",
        "@json_c//:json-c",
    ],
    attributes = pkg_attributes(mode = "0444"),
    prefix = aws_nitro_kms_path,
)

pkg_tar(
    name = "kms_binaries",
    srcs = [
        ":kms_executables",
        ":kms_libs",
    ],
    visibility = ["//visibility:public"],
)

cc_library(
    name = "nontee_aws_kms_client_provider",
    srcs = [
        "aws_kms_aead.cc",
        "nontee_aws_kms_client_provider.cc",
    ],
    hdrs = [
        "aws_kms_aead.h",
        "nontee_aws_kms_client_provider.h",
        "nontee_error_codes.h",
    ],
    deps = [
        "//src/core/async_executor/aws:aws_async_executor",
        "//src/core/common/uuid",
        "//src/core/interface",
        "//src/core/interface:async_context",
        "//src/core/utils:core_utils",
        "//src/cpio/client_providers/interface:cpio_client_providers_interface",
        "//src/cpio/common/aws:aws_utils",
        "//src/public/core/interface:execution_result",
        "//src/public/cpio/interface:cpio_errors",
        "//src/public/cpio/interface/kms_client:type_def",
        "@aws_sdk_cpp//:kms",
        "@tink_cc",
    ],
)

cc_library(
    name = "tee_aws_kms_client_provider",
    srcs = [
        "tee_aws_kms_client_provider.cc",
        "tee_aws_kms_client_provider_utils.cc",
    ],
    hdrs = [
        "tee_aws_kms_client_provider.h",
        "tee_aws_kms_client_provider_utils.h",
        "tee_error_codes.h",
    ],
    defines = [
        'AWS_NITRO_ENCLAVES_CLI_PATH=\\"{path}\\"'.format(
            path = aws_nitro_kms_cli_path,
        ),
    ],
    deps = [
        "//src/core/common/uuid",
        "//src/core/interface",
        "//src/core/interface:async_context",
        "//src/core/utils:core_utils",
        "//src/cpio/client_providers/interface:cpio_client_providers_interface",
        "//src/cpio/common/aws:aws_utils",
        "//src/public/cpio/interface:cpio_errors",
        "//src/public/cpio/interface/kms_client:type_def",
        "@aws_sdk_cpp//:kms",
        "@com_google_absl//absl/status",
        "@com_google_absl//absl/status:statusor",
        "@com_google_absl//absl/strings",
        "@com_google_absl//absl/types:span",
        "@tink_cc",
    ],
)

cc_binary(
    name = "true",
    srcs = ["true_main.cc"],
)

cc_binary(
    name = "false",
    srcs = ["false_main.cc"],
)

cc_binary(
    name = "hello",
    srcs = ["hello_main.cc"],
    deps = [
        "@com_google_absl//absl/flags:flag",
        "@com_google_absl//absl/flags:parse",
    ],
)

cc_test(
    name = "nontee_aws_kms_client_provider_test",
    size = "small",
    srcs = ["nontee_aws_kms_client_provider_test.cc"],
    deps = [
        ":nontee_aws_kms_client_provider",
        "//src/core/async_executor/mock:core_async_executor_mock",
        "//src/core/interface",
        "//src/core/test/utils",
        "//src/cpio/client_providers/kms_client_provider/mock/aws:aws_kms_client_provider_mock",
        "//src/cpio/client_providers/role_credentials_provider/mock:role_credentials_provider_mock",
        "//src/public/core:test_execution_result_matchers",
        "//src/public/core/interface:execution_result",
        "@com_google_absl//absl/synchronization",
        "@com_google_googletest//:gtest_main",
    ],
)

cc_test(
    name = "tee_aws_kms_client_provider_test",
    size = "small",
    srcs = ["tee_aws_kms_client_provider_test.cc"],
    deps = [
        ":tee_aws_kms_client_provider",
        "//src/core/interface",
        "//src/core/test/utils",
        "//src/cpio/client_providers/kms_client_provider/mock/aws:aws_kms_client_provider_mock",
        "//src/cpio/client_providers/role_credentials_provider/mock:role_credentials_provider_mock",
        "//src/public/core:test_execution_result_matchers",
        "//src/public/core/interface:execution_result",
        "@com_google_absl//absl/strings",
        "@com_google_absl//absl/synchronization",
        "@com_google_googletest//:gtest_main",
    ],
)

cc_test(
    name = "aws_kms_aead_test",
    size = "small",
    srcs = ["aws_kms_aead_test.cc"],
    deps = [
        ":nontee_aws_kms_client_provider",
        "//src/core/interface",
        "//src/core/test/utils",
        "//src/cpio/client_providers/kms_client_provider/mock/aws:aws_kms_client_provider_mock",
        "@com_google_googletest//:gtest_main",
    ],
)

cc_test(
    name = "tee_aws_kms_client_provider_utils_test",
    size = "small",
    srcs = ["tee_aws_kms_client_provider_utils_test.cc"],
    data = [
        ":false",
        ":hello",
        ":true",
    ],
    deps = [
        ":tee_aws_kms_client_provider",
        "@com_google_absl//absl/status",
        "@com_google_absl//absl/status:statusor",
        "@com_google_absl//absl/strings",
        "@com_google_googletest//:gtest_main",
    ],
)
