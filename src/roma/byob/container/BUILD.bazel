# Copyright 2024 Google LLC
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

load("@aspect_bazel_lib//lib:copy_file.bzl", "copy_file")
load("@aspect_bazel_lib//lib:expand_template.bzl", "expand_template")
load("@rules_cc//cc:defs.bzl", "cc_binary")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_load")
load("@rules_pkg//pkg:mappings.bzl", "pkg_attributes", "pkg_files", "pkg_mkdirs")
load("@rules_pkg//pkg:tar.bzl", "pkg_tar")
load("//src/roma/byob/config:container.bzl", "roma_container_dir", "roma_container_root_dir")
load("//third_party:container_deps.bzl", "DISTROLESS_USERS")

[
    expand_template(
        name = "config-{}_json".format(arch),
        out = "config-{}.json".format(arch),
        substitutions = {
            "@ld_library_path@": ld_library_path,
        },
        template = ":config.json.tmpl",
    )
    for arch, ld_library_path in (
        ("aarch64", "/lib"),
        ("amd64", "/lib64"),
    )
]

alias(
    name = "config_json",
    actual = select({
        "@platforms//cpu:aarch64": ":config-aarch64.json",
        "@platforms//cpu:x86_64": ":config-amd64.json",
    }),
)

copy_file(
    name = "container_config",
    src = ":config_json",
    out = "config.json",
    # TODO: b/383174230 - Revert once server teams move to rules_oci
    visibility = ["//visibility:public"],
)

pkg_files(
    name = "container_config_file",
    srcs = [":container_config"],
    attributes = pkg_attributes(mode = "0600"),
)

[
    pkg_tar(
        name = "container_config_{}.tar".format(user.user),
        srcs = [":container_config_file"],
        owner = "{}.{}".format(
            user.uid,
            user.gid,
        ),
    )
    for user in DISTROLESS_USERS
]

genrule(
    name = "runsc_bin",
    srcs = [
        ":Dockerfile.runsc",
        ":generate_system_bazelrc",
        ":gvisor.patch",
    ],
    outs = ["runsc"],
    cmd_bash = """set -o errexit
cp $(SRCS) "$(RULEDIR)"
docker buildx build --progress plain --target runsc-binary --output="$(RULEDIR)" --file="$(RULEDIR)"/Dockerfile.runsc "$(RULEDIR)"
""",
)

pkg_files(
    name = "gvisor_execs",
    srcs = [":runsc"],
    attributes = pkg_attributes(mode = "0500"),
    prefix = "/usr/bin",
)

pkg_mkdirs(
    name = "var_run_runsc_dir",
    attributes = pkg_attributes(mode = "0700"),
    dirs = ["/var/run/runsc"],
    # TODO: b/383174230 - Revert once server teams move to rules_oci
    visibility = ["//visibility:public"],
)

[
    pkg_tar(
        name = "gvisor_tar_{}".format(user.flavor),
        srcs = [
            ":gvisor_execs",
            ":var_run_runsc_dir",
        ],
        owner = "{}.{}".format(
            user.uid,
            user.gid,
        ),
        visibility = ["//visibility:public"],
    )
    for user in DISTROLESS_USERS
]

cc_binary(
    name = "run_workers",
    srcs = ["run_workers.cc"],
    deps = [
        "//src/roma/byob/dispatcher",
        "//src/roma/byob/dispatcher:dispatcher_cc_proto",
        "//src/roma/byob/dispatcher:dispatcher_grpc",
        "//src/roma/byob/utility:utils",
        "//src/util/status_macro:status_macros",
        "//src/util/status_macro:status_util",
        "@com_github_grpc_grpc//:grpc++",
        "@com_google_absl//absl/cleanup",
        "@com_google_absl//absl/container:flat_hash_map",
        "@com_google_absl//absl/container:flat_hash_set",
        "@com_google_absl//absl/flags:flag",
        "@com_google_absl//absl/flags:parse",
        "@com_google_absl//absl/log",
        "@com_google_absl//absl/log:check",
        "@com_google_absl//absl/log:initialize",
        "@com_google_absl//absl/strings",
        "@com_google_absl//absl/synchronization",
        "@com_google_absl//absl/types:span",
        "@com_google_protobuf//:protobuf",
        "@libcap",
        "@libuuid//:uuid",
    ],
)

pkg_files(
    name = "byob_runtime_execs",
    srcs = [":run_workers"],
    attributes = pkg_attributes(mode = "0500"),
    prefix = "/server/bin",
)

[
    pkg_tar(
        name = "byob_runtime_tar_{}".format(user.flavor),
        srcs = [":byob_runtime_execs"],
        owner = "{}.{}".format(
            user.uid,
            user.gid,
        ),
    )
    for user in DISTROLESS_USERS
]

alias(
    name = "runtime-debian-nondebug-nonroot",
    actual = select({
        "@platforms//cpu:aarch64": "@runtime-debian-nondebug-nonroot-arm64",
        "@platforms//cpu:x86_64": "@runtime-debian-nondebug-nonroot-amd64",
    }),
)

[
    oci_image(
        name = "byob_runtime_image_{}".format(user.flavor),
        base = ":runtime-debian-nondebug-nonroot",
        cmd = ["/server/bin/run_workers"],
        tars = [":byob_runtime_tar_{}".format(user.flavor)],
    )
    for user in DISTROLESS_USERS
]

_server_image = "byob_server:v1-{flavor}"

[
    oci_load(
        name = "byob_runtime_image_{}_tar".format(user.flavor),
        image = ":byob_runtime_image_{}".format(user.flavor),
        repo_tags = [_server_image.format(flavor = user.flavor)],
    )
    for user in DISTROLESS_USERS
]

[
    filegroup(
        name = "byob_runtime_image_{}.tar".format(user.flavor),
        srcs = [":byob_runtime_image_{}_tar".format(user.flavor)],
        output_group = "tarball",
    )
    for user in DISTROLESS_USERS
]

# warning: this is not hermetic, it uses the local docker client to load an
# image, create a container then export that image using a static container name
[
    genrule(
        name = "gen_byob_runtime_container_tar_{}".format(user.flavor),
        srcs = [":byob_runtime_image_{}.tar".format(user.flavor)],
        outs = ["byob_runtime_container_{}.tar".format(user.flavor)],
        cmd_bash = """
docker load -i "$(location :byob_runtime_image_{flavor}.tar)"
CID=$$(docker create --privileged "{image_uri}")
docker export $$CID -o $@
docker rm $$CID
""".format(
            flavor = user.flavor,
            image_uri = _server_image.format(flavor = user.flavor),
        ),
        visibility = ["//visibility:public"],
    )
    for user in DISTROLESS_USERS
]

[
    genrule(
        name = "gen_byob_runtime_container_with_dir_tar_{}".format(user.flavor),
        srcs = [
            ":byob_runtime_container_{}.tar".format(user.flavor),
            ":container_config_{}.tar".format(user.user),
        ],
        outs = ["byob_runtime_container_with_dir_{}.tar".format(user.flavor)],
        cmd_bash = """
TMPDIR="$$(mktemp --directory roma_container_tmp.XXXXXXXXXX)"
MACHINE="$$(uname --machine)"
mkdir --parents --mode=700 "$$TMPDIR/{roma_container_dir}/{roma_container_root_dir}"
tar --extract --file="$(location :byob_runtime_container_{flavor}.tar)" --directory="$$TMPDIR/{roma_container_dir}/{roma_container_root_dir}"
tar --extract --file="$(location :container_config_{flavor}.tar)" --directory="$$TMPDIR/{roma_container_dir}"

readonly ROOT_DIR="$${{TMPDIR}}/{roma_container_dir}/{roma_container_root_dir}"
if [[ -d $${{ROOT_DIR}}/lib64 ]]; then
  TARGET_DIR="$${{ROOT_DIR}}/lib64"
elif [[ -d $${{ROOT_DIR}}/lib ]]; then
  TARGET_DIR="$${{ROOT_DIR}}/lib"
else
  printf "Error: Expecting either /lib64 or /lib to exist\n" &>/dev/stderr
  exit -1
fi
cp --dereference \
  "$${{ROOT_DIR}}/usr/lib/$${{MACHINE}}-linux-gnu"/libstdc++.so.6 \
  "$${{ROOT_DIR}}/lib/$${{MACHINE}}-linux-gnu"/{{libc.so.6,libgcc_s.so.1,libm.so.6,libpthread.so.0}} \
  "$${{TARGET_DIR}}"
rm -rf "$${{ROOT_DIR}}/usr/lib"
if [[ -d $${{ROOT_DIR}}/lib64 ]]; then
   # move ld.so to /lib64
  mv "$${{ROOT_DIR}}/lib/$${{MACHINE}}-linux-gnu"/ld-2.31.so "$${{ROOT_DIR}}/lib64/ld-linux-$${{MACHINE/_/-}}.so.2"
  rm -rf "$${{ROOT_DIR}}/lib"
fi

tar --create --gzip --owner={user}:{uid} --group={group}:{gid} --file=$@ --directory="$${{TMPDIR}}" "{roma_container_dir}/"
rm -rf "$${{TMPDIR}}"
""".format(
            flavor = user.flavor,
            gid = user.gid,
            group = user.group,
            roma_container_dir = roma_container_dir.replace("/", ""),
            roma_container_root_dir = roma_container_root_dir,
            uid = user.uid,
            user = user.user,
        ),
        visibility = ["//visibility:public"],
    )
    for user in DISTROLESS_USERS
]
